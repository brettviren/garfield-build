CDECK  ID>, FUGLUX.
       SUBROUTINE FUGLUX(ARRAY,XRAN,LEN)
*-----------------------------------------------------------------------
*   FUGLUX - Generation of LEN random numbers in any given distribution,
*            by 4-point interpolation in the inverse cumulative distr.
*            which was previously generated by FUGLXP
*
*   The array ARRAY is assumed to have the following structure:
*        ARRAY(1-100) contains the 99 bins of the inverse cumulative
*                     distribution of the entire function.
*        ARRAY(101-150) contains the 49-bin blowup of main bins
*                       1 and 2 (left tail of distribution)
*        ARRAY(151-200) contains the 49-bin blowup of main bins
*                       98 and 99 (right tail of distribution)
*
*   Origin: V152, Fred James
*-----------------------------------------------------------------------
       implicit none
       INTEGER LEN,IBUF,J,J1
       REAL ARRAY(*),XRAN(LEN),GAP,GAPINV,TLEFT,BRIGHT,GAPS,GAPINS,
     -      X,P,A,B
*** Bin width for main sequence, and its inverse
       PARAMETER (GAP= 1./99.,  GAPINV=99.)
*** Top of left tail, bottom of right tail (each tail replaces 2 bins)
       PARAMETER (TLEFT= 2./99.,BRIGHT=97./99.)
*** Bin width for minor sequences (tails), and its inverse
       PARAMETER (GAPS=TLEFT/49.,  GAPINS=1./GAPS)
*** Draw random numbers.
       CALL RANLUX(XRAN,LEN)
*** Compute random numbers.
       DO 500 IBUF= 1, LEN
       X = XRAN(IBUF)
       J = INT(  X    *GAPINV) + 1
       IF (J .LT. 3)  THEN
            J1 = INT( X *GAPINS)
            J = J1 + 101
            J = MAX(J,102)
            J = MIN(J,148)
            P = (   X -GAPS*(J1-1)) * GAPINS
            A = (P+1.0) * ARRAY(J+2) - (P-2.0)*ARRAY(J-1)
            B = (P-1.0) * ARRAY(J) - P * ARRAY(J+1)
            XRAN(IBUF) = A*P*(P-1.0)*0.16666667  + B*(P+1.0)*(P-2.0)*0.5
       ELSE IF (J .GT. 97)  THEN
            J1 = INT((X-BRIGHT)*GAPINS)
            J = J1 + 151
            J = MAX(J,152)
            J = MIN(J,198)
            P = (X -BRIGHT -GAPS*(J1-1)) * GAPINS
            A = (P+1.0) * ARRAY(J+2) - (P-2.0)*ARRAY(J-1)
            B = (P-1.0) * ARRAY(J) - P * ARRAY(J+1)
            XRAN(IBUF) = A*P*(P-1.0)*0.16666667  + B*(P+1.0)*(P-2.0)*0.5
       ELSE
            P = (   X -GAP*(J-1)) * GAPINV
            A = (P+1.0) * ARRAY(J+2) - (P-2.0)*ARRAY(J-1)
            B = (P-1.0) * ARRAY(J) - P * ARRAY(J+1)
            XRAN(IBUF) = A*P*(P-1.0)*0.16666667  + B*(P+1.0)*(P-2.0)*0.5
       ENDIF
  500  CONTINUE
       END
