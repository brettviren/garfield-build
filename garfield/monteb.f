CDECK  ID>, MONTEB.
      SUBROUTINE MONTEB
      IMPLICIT REAL*8 (A-H,O-Z)
*   Array dimensions.
       integer mxngas
       parameter(mxngas=6)
       INTEGER NGAS,NSTEP,IDBG
       DOUBLE PRECISION EFINAL,ESTEP,AKT,ARY,TEMPC,TORR
       PARAMETER(ARY=13.60569172)
       COMMON/INPT/NGAS,NSTEP,EFINAL,ESTEP,AKT,TEMPC,TORR,IDBG
       DOUBLE PRECISION CONST1,CONST2,CONST3,CONST4,CONST5
       COMMON/CNSTS1/CONST1,CONST2,CONST3,CONST4,CONST5
       DOUBLE PRECISION TMAX,SMALL,API,ESTART,THETA,PHI,TCFMAX,RSTART,
     -      EMAG
       INTEGER NMAX
       COMMON/SETP/TMAX,SMALL,API,ESTART,THETA,PHI,TCFMAX(8),RSTART,
     -      EMAG,NMAX
       DOUBLE PRECISION EOVB,WB,BTHETA,BMAG
       COMMON/BFLD/EOVB,WB,BTHETA,BMAG
*   Sometimes IPLAST is called LAST
       DOUBLE PRECISION CF,EIN,TCF,RGAS,WPL
       INTEGER IARRY,IPN,IPLAST,ISIZE
       COMMON/LARGE/CF(2048,512),EIN(512),TCF(2048),IARRY(512),
     -      RGAS(512),IPN(512),WPL(512),IPLAST,ISIZE
*   Is in effect the old ANCT common.
       DOUBLE PRECISION PSCT,ANGCT
       INTEGER INDEX,NISO
       COMMON/ANIS/PSCT(2048,512),ANGCT(2048,512),INDEX(512),NISO
*   Grouped QIN1 ... QIN6 in QINN
       DOUBLE PRECISION QELM,QSUM,QION,QINN,QSATT
       COMMON /MIX1/ QELM(2048),QSUM(2048),QION(mxngas,2048),
     -      QINn(220,2048,mxngas),QSATT(2048)
*   EVECT is originally called E or ES depending on the routine.
       DOUBLE PRECISION Evect,EROOT,QTOT,QREL,QINEL,QEL
       COMMON /MIX2/ Evect(2048),EROOT(2048),QTOT(2048),QREL(2048),
     -      QINEL(2048),QEL(2048)
*   Extensively reduced.
       INTEGER NINn
*           ,LION,LIN1,LIN2,LIN3,LIN4,LIN5,LIN6
*           DOUBLE PRECISION ALION,ALIN1,ALIN2,ALIN3,ALIN4,ALIN5,ALIN6
       COMMON /MIX3/ NINn(mxngas)
*           ,LION(6),LIN1(220),
*     -      LIN2(220),LIN3(220),LIN4(220),LIN5(220),LIN6(220),ALION(6),
*     -      ALIN1(220),ALIN2(220),ALIN3(220),ALIN4(220),ALIN5(220),
*     -      ALIN6(220)
       DOUBLE PRECISION ALPHA,ATT
       COMMON /CTOWNS/ ALPHA,ATT
       DOUBLE PRECISION ALPER,ATTER
       COMMON /CTWNER/ ALPER,ATTER
       DOUBLE PRECISION DXXER,DYYER,DZZER,DYZER,DXYER,DXZER
       COMMON /DIFERB/ DXXER,DYYER,DZZER,DYZER,DXYER,DXZER
       DOUBLE PRECISION DFLER,DFTER
       COMMON /DIFERL/ DFLER,DFTER
       DOUBLE PRECISION DIFXX,DIFYY,DIFZZ,DIFYZ,DIFXY,DIFXZ
       COMMON /DIFLAB/ DIFXX,DIFYY,DIFZZ,DIFYZ,DIFXY,DIFXZ
       DOUBLE PRECISION DIFLN,DIFTR
       COMMON /DIFVEL/ DIFLN,DIFTR
       DOUBLE PRECISION WX,WY,WZ
       COMMON /VEL/ WX,WY,WZ
       DOUBLE PRECISION DWX,DWY,DWZ
       COMMON /VELERR/ DWX,DWY,DWZ
*   Adjusted size of ICOLL
       DOUBLE PRECISION TIME,SPEC,TMAX1,AVE,DEN,XID,X,Y,Z,ST
       INTEGER ICOLL,NNULL,ICOLN
       COMMON/OUTPT/TIME(300),ICOLL(5*mxngas),SPEC(2048),TMAX1,
     -      AVE,DEN,XID,X,Y,Z,ST,NNULL,ICOLN(512)
*-----------------------------------------------------------------------
*   MAGPAR - Interface parameters for gas mixing with Magboltz.
*   (Last changed on  2/ 3/08.)
*-----------------------------------------------------------------------
       INTEGER MXGNAM
       PARAMETER(MXGNAM=60)
       DOUBLE PRECISION FRAMIX
       LOGICAL LF0PLT,LCSPLT,LGKEEP,LBMCPR
       COMMON /MAGPAR/ FRAMIX(MXGNAM),LF0PLT,LCSPLT,LGKEEP,LBMCPR
       LOGICAL         LINPUT,LCELPR,LCELPL,LWRMRK,LISOCL,LCHGCH,
     -         LDRPLT,LDRPRT,LCLPRT,LCLPLT,LMAPCH,LCNTAM,
     -         LDEBUG,LIDENT,LKEYPL,LRNDMI,LPROPR,LPROF,LGSTOP,LGSIG,
     -         LSYNCH,LINPRD
       INTEGER LUNOUT,JFAIL,JEXMEM
       COMMON /PRTPLT/ LINPUT,LCELPR,LCELPL,LWRMRK,LISOCL,LCHGCH,
     -         LDRPLT,LDRPRT,LCLPRT,LCLPLT,LMAPCH,LCNTAM,
     -         LDEBUG,LIDENT,LKEYPL,LRNDMI,LPROPR,LPROF,LGSTOP,LGSIG,
     -         LSYNCH,LINPRD,LUNOUT,JFAIL,JEXMEM
      DIMENSION XST(100000),YST(100000),ZST(100000),STO(100000)
      DIMENSION WZST(10),WYST(10),AVEST(10)
      DIMENSION DFZZST(10),DFYYST(10),DFXXST(10)
      DIMENSION DFYZST(10),DFLNST(10),DFTRST(10)
C -------------------------------------------------------------------
C   CALCULATES COLLISION EVENTS AND UPDATES DIFFUSION AND VELOCITY.
C   SUBROUTINE HANDLES MAGNETIC FIELD AND ELECTRIC FIELD
C   BFIELD ALONG X-AXIS EFIELD ALONG Z-AXIS (90 DEGREES).
C -------------------------------------------------------------------
      IF(LIDENT)PRINT *,' /// ROUTINE MONTEB ///'
      WX=0.0D0
      DWX=0.0D0
      DIFXZ=0.0D0
      DIFXY=0.0D0
      DXZER=0.0D0
      DXYER=0.0D0
      X=0.0D0
      Y=0.0D0
      Z=0.0D0
      ST=0.0D0
      ST1=0.0D0
      ST2=0.0D0
      SUMXX=0.0D0
      SUMYY=0.0D0
      SUMZZ=0.0D0
      SUMYZ=0.0D0
      SUMLS=0.0D0
      SUMTS=0.0D0
      SUMVX=0.0D0
      ZOLD=0.0D0
      YOLD=0.0D0
      STOLD=0.0D0
      ST1OLD=0.0D0
      ST2OLD=0.0D0
      SZZOLD=0.0D0
      SXXOLD=0.0D0
      SYYOLD=0.0D0
      SYZOLD=0.0D0
      SVXOLD=0.0D0
      SLNOLD=0.0D0
      STROLD=0.0D0
      EBAROLD=0.0D0
      SMALL=1.0D-20
      TMAX1=0.0D0
      EF100=EMAG*100.0D0
      RDUM=RSTART
      E1=ESTART
      INTEM=8
      ITMAX=10
      ID=0
      NCOL=0
      NNULL=0
C  NUMBER OF COLLISIONS FOR DE-CORRELATION
      NCOLM=100000
      TDASH=0.0D0
      CONST9=CONST3*0.01D0
C
C     INITIAL DIRECTION COSINES
C
      DCZ1=COS(THETA)
      DCX1=SIN(THETA)*COS(PHI)
      DCY1=SIN(THETA)*SIN(PHI)
C     INITIAL VELOCITY
      VTOT=CONST9*SQRT(E1)
      CX1=DCX1*VTOT
      CY1=DCY1*VTOT
      CZ1=DCZ1*VTOT
      F4=2.0D0*ACOS(-1.0D0)
      DELTAE=EFINAL/DBLE(INTEM)
      J2M=NMAX/ITMAX
C MAIN LOOP
      DO 210 J1=1,ITMAX
      DO 133 J2=1,J2M
    1 R1=drand48(RDUM)
      I=INT(E1/DELTAE)+1
      I=MIN(I,INTEM)
      TLIM=TCFMAX(I)
      T=-LOG(R1)/TLIM+TDASH
      TDASH=T
      WBT=WB*T
      COSWT=COS(WBT)
      SINWT=SIN(WBT)
      DZ=(CZ1*SINWT+(EOVB-CY1)*(1.0D0-COSWT))/WB
      E=E1+DZ*EF100
C     IF(E.LT.0.0.and.LBMCPR)WRITE(LUNOUT,983)
C    -      J2,DZ,E1,COSWT,SINWT,WBT,CY1
C983  FORMAT(2X,' J2=',I12,' DZ=',D12.3,' E1=',D12.3,' COSWT=',D12.3
C    /,' SINWT=',D12.3,' WBT=',D12.3,' CY1=',D12.3)
      IE=INT(E/ESTEP)+1
      IE=MIN(IE,2048)
      IF(TCF(IE).GT.TLIM) THEN
       TDASH=TDASH+LOG(R1)/TLIM
       TCFMAX(I)=1.05D0*TCFMAX(I)
C
C      IF(LBMCPR)WRITE(LUNOUT,996)
C996   FORMAT(/,5X,' WARNING NULL COLLISION TIME INCREASED',/)
       GO TO 1
      ENDIF
C
C     TEST FOR REAL OR NULL COLLISION
C
      R5=drand48(RDUM)
      TLIM=TCF(IE)/TLIM
      IF(R5.GT.TLIM) THEN
       NNULL=NNULL+1
       GO TO 1
      ENDIF
C
C  CALCULATE DIRECTION COSINES AND POSITIONS AT INSTANT BEFORE COLLISION
C    ALSO UPDATE DIFFUSION  AND ENERGY CALCULATIONS.
      T2=T*T
      IF(T.GE.TMAX1) TMAX1=T
      TDASH=0.0D0
C  CALC VELOCITY
      CX2=CX1
      CY2=(CY1-EOVB)*COSWT+CZ1*SINWT+EOVB
      CZ2=CZ1*COSWT-(CY1-EOVB)*SINWT
C CALC DIRECTION COSINE
      VTOT=SQRT(CX2*CX2+CY2*CY2+CZ2*CZ2)
      DCX2=CX2/VTOT
      DCY2=CY2/VTOT
      DCZ2=CZ2/VTOT
      NCOL=NCOL+1
C CALC NEW POSITION
      X=X+CX1*T
      Y=Y+EOVB*T+((CY1-EOVB)*SINWT+CZ1*(1.0D0-COSWT))/WB
      Z=Z+DZ
      ST=ST+T
      IT=INT(T+1.0D0)
      IT=MIN(IT,300)
      TIME(IT)=TIME(IT)+1.0D0
      SPEC(IE)=SPEC(IE)+1.0D0
      WZ=Z/ST
      WY=Y/ST
      SUMVX=SUMVX+CX1*CX1*T2
      IF(ID.EQ.0) GO TO 121
      KDUM=0
      DO 120 J=1,4
      ST2=ST2+T
      NCOLDM=NCOL+KDUM
      IF(NCOLDM.GT.NCOLM) NCOLDM=NCOLDM-NCOLM
      SDIF=ST-STO(NCOLDM)
      SUMXX=SUMXX+((X-XST(NCOLDM))**2)*T/SDIF
      IF(J1.LT.3) GO TO 120
      ST1=ST1+T
      SUMZZ=SUMZZ+((Z-ZST(NCOLDM)-WZ*SDIF)**2)*T/SDIF
      SUMYY=SUMYY+((Y-YST(NCOLDM)-WY*SDIF)**2)*T/SDIF
      SUMYZ=SUMYZ+(Z-ZST(NCOLDM)-WZ*SDIF)*(Y-YST(NCOLDM)-WY*SDIF)*T/SDIF
      A2=(WZ*SDIF)**2+(WY*SDIF)**2
      B2=(Z-WZ*SDIF-ZST(NCOLDM))**2+(Y-WY*SDIF-YST(NCOLDM))**2
      C2=(Z-ZST(NCOLDM))**2+(Y-YST(NCOLDM))**2
      DL2=(A2+B2-C2)**2/(4.0D0*A2)
      DT2=B2-DL2
      SUMLS=SUMLS+DL2*T/SDIF
      SUMTS=SUMTS+DT2*T/SDIF
 120  KDUM=KDUM+12500
 121  XST(NCOL)=X
      YST(NCOL)=Y
      ZST(NCOL)=Z
      STO(NCOL)=ST
      IF(NCOL.GE.NCOLM) THEN
       ID=ID+1
       XID=DBLE(ID)
       NCOL=0
      ENDIF
C ---------------------------------------------------------------------
C     DETERMINATION OF REAL COLLISION TYPE
C ---------------------------------------------------------------------
      R2=drand48(RDUM)
C FIND LOCATION WITHIN 4 UNITS IN COLLISION ARRAY
      CALL SORT(I,R2,IE)
  140 I=I+1
      IF(CF(IE,I).LT.R2) GO TO 140
      S1=RGAS(I)
      EI=EIN(I)
      IF(IPN(I).LE.0) GO TO 666
C  USE FLAT DISTRIBUTION OF  ELECTRON ENERGY BETWEEN E-EION AND 0.0 EV
C  SAME AS IN BOLTZMANN
      R9=drand48(RDUM)
      EXTRA=R9*(E-EI)
      EI=EXTRA+EI
C
C  GENERATE SCATTERING ANGLES AND UPDATE  LABORATORY COSINES AFTER
C   COLLISION ALSO UPDATE ENERGY OF ELECTRON.
C
  666 IPT=IARRY(I)
      ICOLL(IPT)=ICOLL(IPT)+1
      ICOLN(I)=ICOLN(I)+1
      IF(E.LT.EI) THEN
C      IF(LBMCPR)WRITE(LUNOUT,994) E,EI,J2
C994  FORMAT(2X,' WARNING ENERGY =',F8.3,' LESS THAN ENERGY LOSS EI=',F8
C    /.3,' AT ITER=',I12,' DUE TO BINNING ERROR')
C  FIX ENERGY LOSS SMALLER THAN INCIDENT ENERGY IF ERROR OCCURS
       EI=E-0.0001D0
      ENDIF
      S2=(S1*S1)/(S1-1.0D0)
C  ANISOTROPIC SCATTERING
      IF(INDEX(I).NE.0) THEN
        R31=drand48(RDUM)
        R3=drand48(RDUM)
        F3=1.0D0-R3*ANGCT(IE,I)
        IF(R31.GT.PSCT(IE,I)) F3=-F3
      ELSE
C ISOTROPIC SCATTERING
        R3=drand48(RDUM)
        F3=1.0D0-2.0D0*R3
      ENDIF
      THETA0=ACOS(F3)
      R4=drand48(RDUM)
      PHI0=F4*R4
      F8=SIN(PHI0)
      F9=COS(PHI0)
      ARG1=1.0D0-S1*EI/E
      ARG1=MAX(ARG1,SMALL)
      D=1.0D0-F3*SQRT(ARG1)
      E1=E*(1.0D0-EI/(S1*E)-2.0D0*D/S2)
      E1=MAX(E1,SMALL)
      Q=SQRT((E/E1)*ARG1)/S1
      Q=MIN(Q,1.0D0)
      THETA=ASIN(Q*SIN(THETA0))
      F6=COS(THETA)
      U=(S1-1.0D0)*(S1-1.0D0)/ARG1
      CSQD=F3*F3
      IF(F3.LT.0.0D0.AND.CSQD.GT.U) F6=-1.0D0*F6
      F5=SIN(THETA)
      DCZ2=MIN(DCZ2,1.0D0)
      VTOT=CONST9*SQRT(E1)
      ARGZ=SQRT(DCX2*DCX2+DCY2*DCY2)
      IF(ARGZ.EQ.0.0D0) THEN
C      IF(LBMCPR)WRITE(LUNOUT,9232) ITER,ID,E1
C9232  FORMAT(3X,'WARNING ARGZ= 0.0 AT ITER =',I10,' ID =',I10,' E1=',E1
C    /2.3)
       DCZ1=F6
       DCX1=F9*F5
       DCY1=F8*F5
       GO TO 130
      ENDIF
      DCZ1=DCZ2*F6+ARGZ*F5*F8
      DCY1=DCY2*F6+(F5/ARGZ)*(DCX2*F9-DCY2*DCZ2*F8)
      DCX1=DCX2*F6-(F5/ARGZ)*(DCY2*F9+DCX2*DCZ2*F8)
C CALCULATE VELOCITY VECTORS AFTER COLLISION
  130 CX1=DCX1*VTOT
      CY1=DCY1*VTOT
      CZ1=DCZ1*VTOT
  133 CONTINUE
C   ------------------------------------------
      IF(J1.EQ.1 .and. LBMCPR)WRITE(LUNOUT,201)
  201 FORMAT(/,'   VELZ    VELY  ENERGY   ID  DIFXX   DIFYY   DIFZZ   DI
     /FYZ  DIFLNG  DIFTRN',/)
      WZ=WZ*1.0D+09
      WY=WY*1.0D+09
      IF(ST2.NE.0.0D0) DIFXX=5.0D+15*SUMXX/ST2
      IF(ST1.NE.0.0D0) DIFYY=5.0D+15*SUMYY/ST1
      IF(ST1.NE.0.0D0) DIFZZ=5.0D+15*SUMZZ/ST1
      IF(ST1.NE.0.0D0) DIFYZ=-5.0D+15*SUMYZ/ST1
      IF(ST1.NE.0.0D0) DIFLN=5.0D+15*SUMLS/ST1
      IF(ST1.NE.0.0D0) DIFTR=5.0D+15*SUMTS/ST1
      IF(NISO.EQ.0) DIFXX=5.0D+15*SUMVX/ST
      EBAR=0.0D0
      DO 300 IK=1,2048
*   Changed name of ES to EVECT (RV, 20/4/2005)
  300 EBAR=EBAR+Evect(IK)*SPEC(IK)/TCF(IK)
      AVE=EBAR/ST
      WZST(J1)=(Z-ZOLD)/(ST-STOLD)*1.0D+09
      WYST(J1)=(Y-YOLD)/(ST-STOLD)*1.0D+09
      AVEST(J1)=(EBAR-EBAROLD)/(ST-STOLD)
      EBAROLD=EBAR
      DFZZST(J1)=0.0D0
      DFYYST(J1)=0.0D0
      DFYZST(J1)=0.0D0
      DFLNST(J1)=0.0D0
      DFTRST(J1)=0.0D0
      IF(J1.GT.2) THEN
       DFZZST(J1)=5.0D+15*(SUMZZ-SZZOLD)/(ST1-ST1OLD)
       DFYYST(J1)=5.0D+15*(SUMYY-SYYOLD)/(ST1-ST1OLD)
       DFYZST(J1)=5.0D+15*(SUMYZ-SYZOLD)/(ST1-ST1OLD)
       DFLNST(J1)=5.0D+15*(SUMLS-SLNOLD)/(ST1-ST1OLD)
       DFTRST(J1)=5.0D+15*(SUMTS-STROLD)/(ST1-ST1OLD)
      ENDIF
      DFXXST(J1)=5.0D+15*(SUMXX-SXXOLD)/(ST2-ST2OLD)
      IF(NISO.EQ.0) DFXXST(J1)=5.0D+15*(SUMVX-SVXOLD)/(ST-STOLD)
      ZOLD=Z
      YOLD=Y
      STOLD=ST
      ST1OLD=ST1
      ST2OLD=ST2
      SVXOLD=SUMVX
      SZZOLD=SUMZZ
      SXXOLD=SUMXX
      SYYOLD=SUMYY
      SYZOLD=SUMYZ
      SLNOLD=SUMLS
      STROLD=SUMTS
      IF(LBMCPR)WRITE(LUNOUT,202) WZ,WY,AVE,ID,DIFXX,DIFYY,DIFZZ,
     /DIFYZ,DIFLN,DIFTR
  202 FORMAT(2(F7.2,1X),F7.4,1X,I4,6(F7.1,1X))
C  LOOP
  210 CONTINUE
C CALCULATE ERRORS AND CHECK AVERAGES
      TWZST=0.0D0
      TWYST=0.0D0
      TAVE=0.0D0
      T2WZST=0.0D0
      T2WYST=0.0D0
      T2AVE=0.0D0
      TZZST=0.0D0
      TYYST=0.0D0
      TXXST=0.0D0
      TYZST=0.0D0
      TLNST=0.0D0
      TTRST=0.0D0
      T2ZZST=0.0D0
      T2YYST=0.0D0
      T2XXST=0.0D0
      T2YZST=0.0D0
      T2LNST=0.0D0
      T2TRST=0.0D0
      DO 768 K=1,10
      TWZST=TWZST+WZST(K)
      TWYST=TWYST+WYST(K)
      TAVE=TAVE+AVEST(K)
      T2WZST=T2WZST+WZST(K)*WZST(K)
      T2WYST=T2WYST+WYST(K)*WYST(K)
      T2AVE=T2AVE+AVEST(K)*AVEST(K)
      TXXST=TXXST+DFXXST(K)
      T2XXST=T2XXST+DFXXST(K)*DFXXST(K)
      IF(K.LT.3) GO TO 768
      TZZST=TZZST+DFZZST(K)
      TYYST=TYYST+DFYYST(K)
      TYZST=TYZST+DFYZST(K)
      TLNST=TLNST+DFLNST(K)
      TTRST=TTRST+DFTRST(K)
      T2ZZST=T2ZZST+DFZZST(K)*DFZZST(K)
      T2YYST=T2YYST+DFYYST(K)*DFYYST(K)
      T2YZST=T2YZST+DFYZST(K)*DFYZST(K)
      T2LNST=T2LNST+DFLNST(K)*DFLNST(K)
      T2TRST=T2TRST+DFTRST(K)*DFTRST(K)
  768 CONTINUE
      DWZ=100.0D0*SQRT((T2WZST-TWZST*TWZST/10.0D0)/9.0D0)/WZ
      DWY=100.0D0*SQRT((T2WYST-TWYST*TWYST/10.0D0)/9.0D0)/ABS(WY)
      DEN=100.0D0*SQRT((T2AVE-TAVE*TAVE/10.0D0)/9.0D0)/AVE
      DXXER=100.0D0*SQRT((T2XXST-TXXST*TXXST/10.0D0)/9.0D0)/DIFXX
      DYYER=100.0D0*SQRT((T2YYST-TYYST*TYYST/8.0D0)/7.0D0)/DIFYY
      DZZER=100.0D0*SQRT((T2ZZST-TZZST*TZZST/8.0D0)/7.0D0)/DIFZZ
      DYZER=100.0D0*SQRT((T2YZST-TYZST*TYZST/8.0D0)/7.0D0)/ABS(DIFYZ)
      DFLER=100.0D0*SQRT((T2LNST-TLNST*TLNST/8.0D0)/7.0D0)/DIFLN
      DFTER=100.0D0*SQRT((T2TRST-TTRST*TTRST/8.0D0)/7.0D0)/DIFTR
C CONVERT TO CM/SEC
      WZ=WZ*1.0D05
      WY=WY*1.0D05
C CALCULATE TOWNSEND COEFICIENTS AND ERRORS
      ANCATT=0.0D0
      ANCION=0.0D0
      DO 800 I=1,NGAS
      ANCATT=ANCATT+ICOLL((5*I)-2)
  800 ANCION=ANCION+ICOLL((5*I)-3)
      ATTER=0.0
      IF(ANCATT.EQ.0.0D0) GO TO 810
      ATTER=100.0D0*SQRT(ANCATT)/ANCATT
  810 ATT=ANCATT/(ST*WZ)*1.0D12
      ALPER=0.0D0
      IF(ANCION.EQ.0.0D0) GO TO 820
      ALPER=100.0D0*SQRT(ANCION)/ANCION
  820 ALPHA=ANCION/(ST*WZ)*1.0D12
      END
