CDECK  ID>, GAS60.
      SUBROUTINE GAS60(Q,QIN,NIN,E,EIN,NAME,VIRIAL,EOBY
     /,PEQEL,PEQIN,KEL,KIN,SCRPT)
      IMPLICIT REAL*8 (A-H,O-Z)
       DOUBLE PRECISION PIR2,ECHARG,EMASS,AMU,BOLTZ,BOLTZJ,
     -      AWB,ALOSCH,ABZERO,ATMOS
       PARAMETER(PIR2=8.79735534D-17)
       PARAMETER(ECHARG=1.602176462D-19)
       PARAMETER(EMASS=9.10938188D-31)
       PARAMETER(AMU=1.66053873D-27)
       PARAMETER(BOLTZ=8.617342D-5)
       PARAMETER(BOLTZJ=1.3806503D-23)
       PARAMETER(AWB=1.758820174D10)
       PARAMETER(ALOSCH=2.6867775D19)
       PARAMETER(ABZERO=273.15D0)
       PARAMETER(ATMOS=760.0D0)
       INTEGER NGAS,NSTEP,IDBG
       DOUBLE PRECISION EFINAL,ESTEP,AKT,ARY,TEMPC,TORR
       PARAMETER(ARY=13.60569172)
       COMMON/INPT/NGAS,NSTEP,EFINAL,ESTEP,AKT,TEMPC,TORR,IDBG
      DIMENSION PEQEL(6,2048),PEQIN(220,2048),KIN(220),KEL(6)
      DIMENSION Q(6,2048),QIN(220,2048),E(6),EIN(220)
      DIMENSION XELM(48),YELM(48),XVIBR(21),YVIBR(21),
     /XION(106),YION(106),XATT(10),YATT(10),XDIS1(28),YDIS1(28),
     /XEL(12),YEL(12)
      CHARACTER*30 SCRPT(226)
      CHARACTER*15 NAME
C ELASTIC MOMENTUM TRANSFER X-SECTION
      DATA XELM/0.00,.001,.004,0.01,.014,0.02,0.03,0.04,0.05,0.06,
     /0.07,0.08,0.10,0.12,0.14,0.17,0.20,0.30,0.40,0.50,
     /0.60,0.70,0.80,1.00,1.20,1.40,1.80,2.15,2.65,3.00,
     /4.00,5.00,7.50,10.0,15.0,20.0,40.0,70.0,100.,150.,
     /200.,400.,1000.,2000.,4000.,10000.,20000.,100000./
      DATA YELM/50.0,50.0,48.5,47.0,44.5,41.5,36.0,29.0,24.0,18.0,
     /14.5,11.5,7.50,4.80,2.65,1.60,1.20,0.95,0.95,1.05,
     /1.20,1.50,2.00,4.60,10.0,19.5,29.5,31.5,31.5,28.5,
     /24.5,25.5,23.0,16.0,11.5,9.50,5.00,3.00,1.50,0.90,
     /0.63,0.28,0.18,0.10,0.05,.0048,.0018,.00009/
C  ELASTIC X-SECTION (ONLY KNOWN IN LIMITED RANGE)
      DATA XEL/1.80,2.15,2.65,3.00,4.00,5.00,7.50,10.0,15.0,20.0,
     /40.0,100./
      DATA YEL/30.0,32.0,35.0,36.5,40.0,44.5,47.0,40.0,29.0,21.0,
     /15.0,5.00/
C VIBRATION RESONANCE SHAPE FUNCTION
C GAUSSIAN SHAPE FUNCTION AT 2.35 EV RESONANCE FWHM = 1.5 EV
C PLUS A HIGH ENERGY TAIL
      DATA XVIBR/0.35,0.73,1.00,1.27,1.54,1.81,2.08,2.35,2.80,3.25,
     /3.70,4.15,4.60,5.05,5.50,5.95,10.0,100.,1000.,10000.,
     /100000./
      DATA YVIBR/0.00,0.16,0.47,1.14,2.25,3.65,4.90,5.40,4.90,3.65,
     /2.25,1.14,0.47,0.16,0.08,0.06,0.05,.005,.0001,.00001,
     /.000001/
C USE BEB VALUES FOR IONISATION
      DATA XION/11.65,12.0,12.5,13.0,13.5,14.0,14.5,15.0,15.5,16.0,
     /16.5,17.0,17.5,18.0,18.5,19.0,19.5,20.0,20.5,21.0,
     /21.5,22.0,22.5,23.0,23.5,24.0,26.0,28.0,30.0,32.0,
     /34.0,36.0,38.0,40.0,45.0,50.0,55.0,60.0,65.0,70.0,
     /75.0,80.0,85.0,90.0,95.0,100.,105.,110.,115.,120.,
     /125.,130.,135.,140.,145.,150.,160.,170.,180.,190.,
     /200.,210.,220.,230.,240.,250.,300.,350.,400.,450.,
     /500.,550.,600.,650.,700.,750.,800.,850.,900.,950.,
     /1000.,1100.,1200.,1300.,1400.,1500.,1600.,1700.,1800.,1900.,
     /2000.,2200.,2400.,2700.,3000.,4000.,5000.,6000.,8000.,10000.,
     /14000.,20000.,30000.,40000.,60000.,100000./
      DATA YION/0.00,.112,.276,.444,.613,.780,.945,1.11,1.26,1.42,
     /1.56,1.70,1.84,1.97,2.10,2.22,2.34,2.45,2.57,2.68,
     /2.79,2.90,3.00,3.10,3.20,3.29,3.62,3.90,4.14,4.34,
     /4.51,4.65,4.78,4.88,5.06,5.17,5.23,5.25,5.24,5.21,
     /5.17,5.12,5.06,5.00,4.93,4.87,4.80,4.73,4.66,4.59,
     /4.52,4.45,4.39,4.32,4.26,4.20,4.08,3.96,3.86,3.75,
     /3.66,3.56,3.48,3.39,3.31,3.24,2.90,2.64,2.42,2.24,
     /2.08,1.94,1.83,1.73,1.64,1.56,1.48,1.42,1.36,1.30,
     /1.25,1.16,1.09,1.02,.963,.912,.866,.825,.788,.754,
     /.724,.670,.623,.566,.518,.407,.337,.288,.225,.185,
     /.138,.101,.070,.054,.038,.024/
C   ATTACHMENT X-SECTION ( NB  *10**19 )
      DATA XATT/7.00,7.50,8.00,8.50,9.00,9.50,10.0,10.5,11.0,11.5/
      DATA YATT/0.00,5.00,10.0,16.0,10.0,5.00,1.00,0.10,0.01,.001/
C  EXCITATION AND DISSOCIATION
      DATA XDIS1/8.00,9.00,10.0,12.0,15.0,20.0,25.0,30.0,40.0,60.0,
     /80.0,100.,150.,200.,300.,400.,500.,750.,1000.,1500.,
     /2000.,3000.,4000.,6000.,8000.,10000.,20000.,100000./
      DATA YDIS1/0.00,2.00,3.80,5.10,5.90,6.60,6.80,6.80,6.80,6.60,
     /6.00,5.50,4.80,4.00,3.00,2.50,2.00,1.40,1.00,0.80,
     /0.60,0.40,0.30,0.20,0.18,0.16,0.08,0.02/
C
C  *********************************************************************
C FIT TO SILANE:
C   DRIFT VELOCITY :
C           W.J.POLLOCK         TRANS FARADAY SOC. 64(1968)2919
C  TRANSVERSE DIFF :
C        MILLICAN AND WALKER    J.PHYS.D 20(1987)193
C  TOWNSEND COEFICIENTS :
C     SHIMOZUMA AND TAGASHIRA   J.PHYS.D 19(1986)L179
C  -----------------------------------------------------------
C  FIT TO MIXTURE DATA :
C  ARGON/SILANE: DL AND VD
C      KURACHI AND NAKAMURA     J.PHYS.D 21(1988)602
C ARGON/KRYPTON: DL AND VD
C      KURACHI AND NAKAMURA     IEEE TRANS PLASMA SCI. 19(1991)262
C
C***********************************************************************
      NAME='Silane (2005)'
C***********************************************************************
      NIN=6
      DO 1 J=1,6
    1 KEL(J)=0
      DO 2 J=1,NIN
    2 KIN(J)=0
C
C  USE ANISOTROPIC SCATTERING FROM LEVEL 2 AND 3 (V24 AND V13)
      KIN(2)=1
      KIN(3)=1
C
      RAT=1.0
      NDATA=48
      NVIBR=21
      NION=106
      NATT=10
      NDIS1=28
      E(1)=0.0
      E(2)=2.0*EMASS/(32.1173*AMU)
      E(3)=11.65
      E(4)=0.0
      E(5)=0.0
      E(6)=0.0
C OPAL AND BEATY ENERGY SPLITTING FACTOR
      EOBY=E(3)
      EIN(1)=-0.1128
      EIN(2)=0.1128
      EIN(3)=0.2707
      EIN(4)=0.35
      EIN(5)=0.50
      EIN(6)=8.0
      SCRPT(1)='                              '
      SCRPT(2)=' ELASTIC        SILANE        '
      SCRPT(3)=' IONISATION    ELOSS= 11.65   '
      SCRPT(4)=' ATTACHMENT                   '
      SCRPT(5)='                              '
      SCRPT(6)='                              '
      SCRPT(7)=' VIB V2+V4     ELOSS= -0.1128 '
      SCRPT(8)=' VIB V2+V4     ELOSS=  0.1128 '
      SCRPT(9)=' VIB V1+V3     ELOSS=  0.2707 '
      SCRPT(10)=' VIB  HAR      ELOSS=  0.350  '
      SCRPT(11)=' VIB  HAR      ELOSS=  0.500  '
      SCRPT(12)=' EXC+DISSOC    ELOSS=  8.0    '
      APOP=DEXP(EIN(1)/AKT)
C
      EN=-ESTEP/2.0D0
      DO 1000 I=1,NSTEP
      EN=EN+ESTEP
C USE LOG INTERPOLATION FOR ELASTIC
      IF(EN.LE.XELM(2)) THEN
       QMOM=YELM(2)*1.D-16
       GO TO 30
      ENDIF
      DO 3 J=2,NDATA
      IF(EN.LE.XELM(J)) GO TO 4
   3  CONTINUE
      J=NDATA
   4  XNJ=DLOG(XELM(J))
      XNJ1=DLOG(XELM(J-1))
      YXJ=DLOG(YELM(J))
      YXJ1=DLOG(YELM(J-1))
      A=(YXJ-YXJ1)/(XNJ-XNJ1)
      B=(XNJ1*YXJ-XNJ*YXJ1)/(XNJ1-XNJ)
      QMOM=DEXP(A*DLOG(EN)+B)*1.D-16
   30 CONTINUE
      PEQEL(2,I)=0.5D0
      Q(2,I)=QMOM
C
C IONISATION
C
      Q(3,I)=0.0D0
      PEQEL(3,I)=0.5D0
      IF(EN.LT.E(3)) GO TO 200
      DO 110 J=2,NION
      IF(EN.LE.XION(J)) GO TO 120
  110 CONTINUE
      J=NION
  120 A=(YION(J)-YION(J-1))/(XION(J)-XION(J-1))
      B=(XION(J-1)*YION(J)-XION(J)*YION(J-1))/(XION(J-1)-XION(J))
      Q(3,I)=(A*EN+B)*1.D-16
C
C ATTACHMENT
C
  200 Q(4,I)=0.0D0
      IF(EN.LE.XATT(1)) GO TO 300
      IF(EN.GT.XATT(10)) GO TO 300
      DO 210 J=2,NATT
      IF(EN.LE.XATT(J)) GO TO 220
  210 CONTINUE
      J=NATT
  220 A=(YATT(J)-YATT(J-1))/(XATT(J)-XATT(J-1))
      B=(XATT(J-1)*YATT(J)-XATT(J)*YATT(J-1))/(XATT(J-1)-XATT(J))
      Q(4,I)=(A*EN+B)*1.D-19
C
  300 Q(5,I)=0.0D0
      Q(6,I)=0.0D0
C
C V4 + V2 SUPERELASTIC
      QIN(1,I)=0.0D0
      QRES=0.0D0
      IF(EN.LE.0.0) GO TO 350
      IF((EN+EIN(2)).LE.XVIBR(1)) GO TO 330
      DO 310 J=2,NVIBR
      IF((EN+EIN(2)).LE.XVIBR(J)) GO TO 320
  310 CONTINUE
      J=NVIBR
  320 A=(YVIBR(J)-YVIBR(J-1))/(XVIBR(J)-XVIBR(J-1))
      B=(XVIBR(J-1)*YVIBR(J)-XVIBR(J)*YVIBR(J-1))/(XVIBR(J-1)-XVIBR(J))
      QRES=1.40*((EN+EIN(2))*(A*(EN+EIN(2))+B)/EN)
  330 CONTINUE
      EFAC=DSQRT(1.0-(EIN(1)/EN))
      QDIPOL=0.660*DLOG((EFAC+1.0)/(EFAC-1.0))/EN
      QIN(1,I)=(QRES+QDIPOL)*1.D-16
      QIN(1,I)=QIN(1,I)*APOP/(1.0+APOP)
  350 CONTINUE
C
C V4 + V2
      QIN(2,I)=0.0D0
      QRES=0.0D0
      IF(EN.LE.EIN(2)) GO TO 400
      IF(EN.LE.XVIBR(1)) GO TO 380
      DO 360 J=2,NVIBR
      IF(EN.LE.XVIBR(J)) GO TO 370
  360 CONTINUE
      J=NVIBR
  370 A=(YVIBR(J)-YVIBR(J-1))/(XVIBR(J)-XVIBR(J-1))
      B=(XVIBR(J-1)*YVIBR(J)-XVIBR(J)*YVIBR(J-1))/(XVIBR(J-1)-XVIBR(J))
      QRES=1.40*(A*EN+B)
  380 CONTINUE
      EFAC=DSQRT(1.0-(EIN(2)/EN))
      QIN(2,I)=0.660*DLOG((1.0+EFAC)/(1.0-EFAC))/EN
      ELF=EN-EIN(2)
      FWD=DLOG((EN+ELF)/(EN+ELF-2.0*DSQRT(EN*ELF)))
      BCK=DLOG((EN+ELF+2.0*DSQRT(EN*ELF))/(EN+ELF))
C RATIO OF MT TO TOTAL X-SECT FOR RESONANCE PART = RAT
      XMT=((1.5-FWD/(FWD+BCK))*QIN(2,I)+RAT*QRES)*1.D-16
      QIN(2,I)=(QIN(2,I)+QRES)*1.D-16
      PEQIN(2,I)=0.5+(QIN(2,I)-XMT)/QIN(2,I)
      QIN(2,I)=QIN(2,I)/(1.0+APOP)
  400 CONTINUE
C
C V1 + V3
      QIN(3,I)=0.0D0
      QRES=0.0D0
      IF(EN.LE.EIN(3)) GO TO 500
      IF(EN.LE.XVIBR(1)) GO TO 430
      DO 410 J=2,NVIBR
      IF(EN.LE.XVIBR(J)) GO TO 420
  410 CONTINUE
      J=NVIBR
  420 A=(YVIBR(J)-YVIBR(J-1))/(XVIBR(J)-XVIBR(J-1))
      B=(XVIBR(J-1)*YVIBR(J)-XVIBR(J)*YVIBR(J-1))/(XVIBR(J-1)-XVIBR(J))
      QRES=0.65*(A*EN+B)
  430 CONTINUE
      EFAC=DSQRT(1.0-(EIN(3)/EN))
      QIN(3,I)=0.418*DLOG((1.0+EFAC)/(1.0-EFAC))/EN
      ELF=EN-EIN(3)
      FWD=DLOG((EN+ELF)/(EN+ELF-2.0*DSQRT(EN*ELF)))
      BCK=DLOG((EN+ELF+2.0*DSQRT(EN*ELF))/(EN+ELF))
C RATIO OF MT TO TOTAL X-SECT FOR RESONANCE PART = RAT
      XMT=((1.5-FWD/(FWD+BCK))*QIN(3,I)+RAT*QRES)*1.D-16
      QIN(3,I)=(QIN(3,I)+QRES)*1.D-16
      PEQIN(3,I)=0.5+(QIN(3,I)-XMT)/QIN(3,I)
  500 CONTINUE
C
C  VIBRATION HARMONICS 1
      QIN(4,I)=0.0D0
      IF(EN.LE.EIN(4)) GO TO 600
      DO 510 J=2,NVIBR
      IF(EN.LE.XVIBR(J)) GO TO 520
  510 CONTINUE
      J=NVIBR
  520 A=(YVIBR(J)-YVIBR(J-1))/(XVIBR(J)-XVIBR(J-1))
      B=(XVIBR(J-1)*YVIBR(J)-XVIBR(J)*YVIBR(J-1))/(XVIBR(J-1)-XVIBR(J))
      QIN(4,I)=0.134*(A*EN+B)*1.D-16
  600 CONTINUE
C
C  VIBRATION HARMONICS 2
      QIN(5,I)=0.0D0
      IF(EN.LE.EIN(5)) GO TO 700
      DO 610 J=2,NVIBR
      IF(EN.LE.XVIBR(J)) GO TO 620
  610 CONTINUE
      J=NVIBR
  620 A=(YVIBR(J)-YVIBR(J-1))/(XVIBR(J)-XVIBR(J-1))
      B=(XVIBR(J-1)*YVIBR(J)-XVIBR(J)*YVIBR(J-1))/(XVIBR(J-1)-XVIBR(J))
      QIN(5,I)=0.094*(A*EN+B)*1.D-16
  700 CONTINUE
C
C  EXCITATION + DISSOCIATION
      QIN(6,I)=0.0D0
      IF(EN.LE.EIN(6)) GO TO 850
      DO 810 J=2,NDIS1
      IF(EN.LE.XDIS1(J)) GO TO 820
  810 CONTINUE
      J=NDIS1
  820 A=(YDIS1(J)-YDIS1(J-1))/(XDIS1(J)-XDIS1(J-1))
      B=(XDIS1(J-1)*YDIS1(J)-XDIS1(J)*YDIS1(J-1))/(XDIS1(J-1)-XDIS1(J))
      QIN(6,I)=(A*EN+B)*1.D-16
  850 CONTINUE
C
      Q(1,I)=Q(2,I)+Q(3,I)+Q(4,I)+QIN(1,I)+QIN(2,I)+QIN(3,I)+QIN(4,I)+
     /QIN(5,I)+QIN(6,I)
C
 1000 CONTINUE
C  SAVE COMPUTE TIME
      IF(EFINAL.LE.EIN(6)) NIN=5
      IF(EFINAL.LE.EIN(5)) NIN=4
      IF(EFINAL.LE.EIN(4)) NIN=3
      IF(EFINAL.LE.EIN(3)) NIN=2
      IF(EFINAL.LE.EIN(2)) NIN=1
C
      RETURN
      END
