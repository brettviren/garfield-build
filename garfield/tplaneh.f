CDECK  ID>, TPLANEH.
      SUBROUTINE TPLANEH(T,E1,CX1,CY1,CZ1,EFLD,IPLANE)
      IMPLICIT REAL*8 (A-H,O-Z)
*   Array dimensions.
       integer mxngas
       parameter(mxngas=6)
       DOUBLE PRECISION CONST1,CONST2,CONST3,CONST4,CONST5
       COMMON/CNSTS1/CONST1,CONST2,CONST3,CONST4,CONST5
       DOUBLE PRECISION EOVB,WB,BTHETA,BMAG
       COMMON/BFLD/EOVB,WB,BTHETA,BMAG
       DOUBLE PRECISION RCS,RSN,EFZ100,EFX100,F1,EOVBR
       COMMON/ROTS/RCS,RSN,EFZ100,EFX100,F1,EOVBR
       DOUBLE PRECISION ALPHAST,VDST,TSTEP,ZSTEP,TFINAL,ZFINAL
       INTEGER ITFINAL,IPRIM
       COMMON/CION/ALPHAST,VDST,TSTEP,ZSTEP,TFINAL,ZFINAL,ITFINAL,IPRIM
*   Adjusted size of ICOLL
       DOUBLE PRECISION TIME,SPEC,TMAX1,AVE,DEN,XID,X,Y,Z,ST
       INTEGER ICOLL,NNULL,ICOLN
       COMMON/OUTPT/TIME(300),ICOLL(5*mxngas),SPEC(2048),TMAX1,
     -      AVE,DEN,XID,X,Y,Z,ST,NNULL,ICOLN(512)
*   Combined /TPLOUT/, /TPLOUTG/, /TPLOUTH/ in a single common.
       DOUBLE PRECISION ETPL,XTPL,YTPL,ZTPL,TTPL,XXTPL,YYTPL,ZZTPL,
     -      YZTPL,XZTPL,XYTPL,VZTPL,VYTPL,VXTPL,ATTOINT,ATTERT,AIOERT
       INTEGER NETPL
       COMMON /TPLOUT/ ETPL(8),XTPL(8),YTPL(8),ZTPL(8),TTPL(8),XXTPL(8),
     -      YYTPL(8),ZZTPL(8),YZTPL(8),XZTPL(8),XYTPL(8),
     -      VZTPL(8),VYTPL(8),VXTPL(8),ATTOINT,ATTERT,AIOERT,NETPL(8)
C-----------------------------------------------------------------------
C STORES POSITION, TIME AND ENERGY AND SUMS REQUIRED
C TO CALCULATE DEVIATIONS AND MEANS AT PLANE =IPLANE
C USED WITH BFIELD AT ANGLE BTHETA TO EMAG
C ROTATES STORED POSITIONS INTO LAB FRAME.
C-----------------------------------------------------------------------
      TIMESP=IPLANE*TSTEP
C CALC TIME LEFT TO ARRIVE AT PLANE
      TIMLFT=TIMESP-ST
      T2LFT=TIMLFT*TIMLFT
      WBT=WB*TIMLFT
      COSWT=COS(WBT)
      SINWT=SIN(WBT)
      CX2=CX1+2.0D0*F1*TIMLFT
      CY2=(CY1-EOVBR)*COSWT+CZ1*SINWT+EOVBR
      CZ2=CZ1*COSWT-(CY1-EOVBR)*SINWT
      VTOT=SQRT(CX2*CX2+CY2*CY2+CZ2*CZ2)
      DCZ2=CZ2/VTOT
      DCY2=CY2/VTOT
      DCX2=CX2/VTOT
      DX=CX1*TIMLFT+F1*TIMLFT*TIMLFT
      XPLANE=X+DX
      YPLANE=Y+EOVBR*TIMLFT+((CY1-EOVBR)*SINWT+CZ1*(1.0D0-COSWT))/WB
      DZ=(CZ1*SINWT+(EOVBR-CY1)*(1.0D0-COSWT))/WB
      ZPLANE=Z+DZ
C ROTATE POSITIONS
      ZPLANER=ZPLANE*RCS-XPLANE*RSN
      YPLANER=YPLANE
      XPLANER=ZPLANE*RSN+XPLANE*RCS
      EPLANE=E1+DZ*EFZ100+DX*EFX100
      VZPLANE=DCZ2*SQRT(EPLANE)*CONST3*0.01D0
      VYPLANE=DCY2*SQRT(EPLANE)*CONST3*0.01D0
      VXPLANE=DCX2*SQRT(EPLANE)*CONST3*0.01D0
      XTPL(IPLANE)=XTPL(IPLANE)+XPLANER
      YTPL(IPLANE)=YTPL(IPLANE)+YPLANER
      ZTPL(IPLANE)=ZTPL(IPLANE)+ZPLANER
      XXTPL(IPLANE)=XXTPL(IPLANE)+XPLANER*XPLANER
      YYTPL(IPLANE)=YYTPL(IPLANE)+YPLANER*YPLANER
      ZZTPL(IPLANE)=ZZTPL(IPLANE)+ZPLANER*ZPLANER
      YZTPL(IPLANE)=YZTPL(IPLANE)+YPLANER*ZPLANER
      XZTPL(IPLANE)=XZTPL(IPLANE)+XPLANER*ZPLANER
      XYTPL(IPLANE)=XYTPL(IPLANE)+XPLANER*YPLANER
      ETPL(IPLANE)=ETPL(IPLANE)+EPLANE
      TTPL(IPLANE)=TTPL(IPLANE)+ST+TIMLFT
C ROTATE VELOCITIES
      VZPLNER=VZPLANE*RCS-VXPLANE*RSN
      VYPLNER=VYPLANE
      VXPLNER=VZPLANE*RSN+VXPLANE*RCS
      VZTPL(IPLANE)=VZTPL(IPLANE)+VZPLNER
      VYTPL(IPLANE)=VYTPL(IPLANE)+VYPLNER
      VXTPL(IPLANE)=VXTPL(IPLANE)+VXPLNER
      NETPL(IPLANE)=NETPL(IPLANE)+1
      END
